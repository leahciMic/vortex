import parse;
import tryConvertFromU64;

func NonEmptyLine(lines, [i, j]) {
  if (i >= lines:Length()) {
    return null;
  }

  line := lines[i];

  if (j >= line:Length()) {
    return null;
  }

  return line;
};

func Digit(s) => switch {
  (s == '0') => 0u64;
  (s == '1') => 1u64;
  (s == '2') => 2u64;
  (s == '3') => 3u64;
  (s == '4') => 4u64;
  (s == '5') => 5u64;
  (s == '6') => 6u64;
  (s == '7') => 7u64;
  (s == '8') => 8u64;
  (s == '9') => 9u64;

  true => null;
};

func isNull(x) => x:String() == 'null';

func RawU64(lines, [i, j]) {
  line := NonEmptyLine(lines, [i, j]);

  if (isNull(line)) {
    return null;
  }

  res := null;

  for {
    digit := Digit(line[j]);

    if (isNull(digit)) {
      break;
    }

    if (isNull(res)) {
      res = digit;
    } else {
      before := res;

      res *= 10u64;
      res += digit;

      if (res < before) {
        return [['overflow'], [i, j]];
      }
    }

    j++;

    if (j >= line:Length()) {
      break;
    }
  }

  if (isNull(res)) {
    return null;
  }

  return [['literal', res], [i, j]];
};

func NumberU64(lines, [i, j]) {
  output := RawU64(lines, [i, j]);

  if (isNull(output)) {
    return null;
  }

  literal := null;
  [literal, [i, j]] = output;

  suffix := parse.Exact('u64')(lines, [i, j]);

  if (isNull(suffix)) {
    return null;
  }

  [i, j] = suffix:Back();

  return [literal, [i, j]];
};

func NumberConvertedUnsigned(suffix) => func(lines, [i, j]) {
  output := RawU64(lines, [i, j]);

  if (isNull(output)) {
    return null;
  }

  literal := null;
  [literal, [i, j]] = output;

  suffixParsed := parse.Exact(suffix)(lines, [i, j]);

  if (isNull(suffixParsed)) {
    return null;
  }

  [i, j] = suffixParsed:Back();

  value := literal[1u64];
  value = tryConvertFromU64(value, suffix);

  if (isNull(value)) {
    return [['overflow'], [i, j]];
  }

  literal[1u64] = value;

  return [literal, [i, j]];
};

func NumberConvertedSigned(lines, [i, j]) {
  minus := parse.Exact('-')(lines, [i, j]);
  isNegated := false;

  if (!isNull(minus)) {
    isNegated = true;
    [i, j] = minus:Back();
  }

  raw := RawU64(lines, [i, j]);

  if (isNull(raw)) {
    return null;
  }

  value := null;
  [value, [i, j]] = raw;

  parsedSuffix := parse.Select([
    parse.Exact('i8'),
    parse.Exact('i16'),
    parse.Exact('i32'),
    parse.Exact('i64'),
  ])(lines, [i, j]);

  suffix := 'i32';

  if (!isNull(parsedSuffix)) {
    [suffix, [i, j]] = parsedSuffix;
  }

  value[1u64] = tryConvertFromU64(value[1u64], suffix);

  if (isNull(value[1u64])) {
    return [['overflow'], [i, j]];
  }

  if (isNegated) {
    value[1u64] = -value[1u64];
  }

  return [value, [i, j]];
};

return parse.Select([
  NumberConvertedUnsigned('u8'),
  NumberConvertedUnsigned('u16'),
  NumberConvertedUnsigned('u32'),
  NumberU64,
  NumberConvertedSigned,
]);
